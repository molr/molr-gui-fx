import org.gradle.api.JavaVersion

buildscript {
    project.ext['CERN_VM'] = System.getProperty('CERN_TECHNET_VM') ?: System.getenv('CERN_TECHNET_VM') ?: System.getProperty('CERN_VM') ?: System.getenv('CERN_VM') ?: false
    project.ext['DEPLOYMENT'] = System.getProperty('deployment') ?: false
    project.ext['VCS_TAG'] = System.getProperty('TRAVIS_TAG') ?: System.getenv('TRAVIS_TAG') ?: System.getProperty('VCS_TAG')

    project.ext['BINTRAY.userName'] = System.getProperty('BINTRAY_USER') ?: System.getenv('BINTRAY_USER')
    project.ext['BINTRAY.apiToken'] = System.getProperty('BINTRAY_API_TOKEN') ?: System.getenv('BINTRAY_API_TOKEN')

    project.ext['SONATYPE.userName'] = System.getProperty('SONATYPE_USER') ?: System.getenv('SONATYPE_USER')
    project.ext['SONATYPE.password'] = System.getProperty('SONATYPE_PASSWORD') ?: System.getenv('SONATYPE_PASSWORD')

    repositories {
        if (project['CERN_VM']) {
            maven { url 'http://artifactory.cern.ch/ds-jcenter' }
            maven { url 'http://artifactory.cern.ch/development' }
        } else {
            mavenCentral()
            jcenter()
            maven { url 'https://plugins.gradle.org/m2/' }
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.jk1:gradle-license-report:1.5'
    }
}

plugins {
    id 'org.openjfx.javafxplugin' version '0.0.7' apply false
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'org.openjfx.javafxplugin'

sourceCompatibility = JavaVersion.VERSION_1_8

group = "io.molr"

repositories {
    repositories {
        if (project['CERN_VM']) {
            maven { url 'http://artifactory.cern.ch/ds-jcenter' }
            maven { url 'http://artifactory.cern.ch/development' }
        } else {
            mavenCentral()
            jcenter()
            maven { url "https://dl.bintray.com/molr/molr-repo" }
        }
    }
}

dependencies {
    /* This is finally the only API we should need... ?*/
    compile group: 'io.molr', name: 'molr-mole-remote', version: molrVersion

    compile group: 'org.minifx', name: 'minifx-workbench', version: '1.1.1-11'
    compile 'de.jensd:fontawesomefx-commons:11.0'

    compile group: 'org.controlsfx', name: 'controlsfx', version: '11.0.1'

    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: springBootVersion) {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'
}

javadoc { options.encoding = "UTF-8" }

if (!project.tasks.findByName("javadocJar")) {
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }
}

if (!project.tasks.findByName("sourcesJar")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}


jacoco.toolVersion = "0.8.5"

jacocoTestReport.reports {
    xml.enabled true
    xml.destination new File("${buildDir}/reports/jacoco/report.xml")
    html.enabled false
    csv.enabled false
}


sourceSets {
    main {
        resources {
            srcDirs "src/main/resources", "src/main/java"
        }
    }
}

if (!project['CERN_VM']) {
    println 'Applying licensing report'
    apply plugin: 'com.github.jk1.dependency-license-report'

    licenseReport {
        renderers = [this.class.classLoader.loadClass('com.github.jk1.license.render.InventoryHtmlReportRenderer').newInstance()]
        filters = [this.class.classLoader.loadClass('com.github.jk1.license.filter.LicenseBundleNormalizer').newInstance()]
    }
}


if (project['DEPLOYMENT']) {
    println "Applying deployment for ${project.name}"
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply from: 'https://raw.githubusercontent.com/ossgang/gradle-scripts/master/deployment/bintray-deploy.gradle'
}

javafx {
    // version = "12.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.web' ]
}

task updatePom {
    doLast {
        def pomInfo = pom {
            project {
                parent {
                    groupId 'io.molr'
                    artifactId 'molr-gui-fx'
                    version 'undefined'
                }
                inceptionYear '2019'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }


        pomInfo.writeTo("pom-generated.xml")
    }
}

task wrapper(type: Wrapper) { gradleVersion = '4.10.2' }
